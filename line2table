#!/usr/bin/awk -f

function usage() {
	print "line2table : line to table converter."	> "/dev/stderr"
	print ""					> "/dev/stderr"

	print "  Example:"				> "/dev/stderr"
	print ""					> "/dev/stderr"
	print "    <input>"				> "/dev/stderr"
	print "        a x 1"				> "/dev/stderr"
	print "        a y 2"				> "/dev/stderr"
	print "        a z 3"				> "/dev/stderr"
	print "        b x 4"				> "/dev/stderr"
	print "        b y 5"				> "/dev/stderr"
	print "        b z 6"				> "/dev/stderr"
	print ""					> "/dev/stderr"
	print "    <output>"				> "/dev/stderr"
	print "        #       x       y       z"	> "/dev/stderr"
	print "        a       1       2       3"	> "/dev/stderr"
	print "        b       4       5       6"	> "/dev/stderr"
	print ""					> "/dev/stderr"

	exit 0
}

BEGIN {
	if (ARGV[1] == "-h") {
		usage()
	}

	col_title_set = 0
	num_key = 0
	num_col = 0
}

NF != 3 {
	printf "line2tab: Warnig: Irregular NF. FILENAME=\"%s\" FNR=%d\n",
	       FILENAME, FNR > "/dev/stderr"
}

prev_key != $1 {
	key[num_key] = $1
	num_key++
}

{
	val[$1, $2] = $3
	prev_key = $1
}

! col_title_set {
	if (old_key != "" && old_key != $1) {
		col_title_set = 1
		next
	}
	old_key = $1

	col_title[num_col] = $2
	num_col++
}

END {
	if (! NR) exit 0;

	OFS = (FS == " " ? "\t" : FS)
	ORS = ""

	print "#"
	for (i = 0; i < num_col; i++)
		print "", col_title[i]
	print "\n"

	for (i = 0; i < num_key; i++) {
		print key[i]
		for (j = 0; j < num_col; j++)
			print "", val[key[i], col_title[j]]
		print "\n"
	}
}
