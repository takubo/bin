#!/usr/bin/awk -f
BEGIN {
	col_title_set = 0
	num_key = 0
	num_col = 0
}

NF != 3 {
	printf "line2tab: Warnig: Irregular NF. FILENAME=\"%s\" FNR=%d\n",
	       FILENAME, FNR > "/dev/stderr"
}

prev_key != $1 {
	key[num_key] = $1
	num_key++
}

{
	val[$1, $2] = $3
	prev_key = $1
}

! col_title_set {
	if (old_key != "" && old_key != $1) {
		col_title_set = 1
		next
	}
	old_key = $1

	col_title[num_col] = $2
	num_col++
}

END {
	OFS = (FS == " " ? "\t" : FS)
	ORS = ""

	print "#"
	for (i = 0; i < num_col; i++)
		print "", col_title[i]
	print "\n"

	for (i = 0; i < num_key; i++) {
		print key[i]
		for (j = 0; j < num_col; j++)
			print "", val[key[i], col_title[j]]
		print "\n"
	}
}
